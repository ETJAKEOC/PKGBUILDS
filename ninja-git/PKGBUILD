pkgname=ninja-git
epoch=2
pkgver=1.13.2.r32.gc74beb43
pkgrel=1
pkgdesc='Small build system with a focus on speed'
arch=(x86_64)
url='https://ninja-build.org/'
license=(Apache)
depends=(gcc-libs)
makedepends=(git cmake python re2c)
provides=(ninja)
conflicts=(ninja)
source=('git+https://github.com/ninja-build/ninja.git')
md5sums=('SKIP')
options=('!debug' '!docs' '!emptydirs' '!staticlibs' 'ccache' 'buildflags' 'lto' 'makeflags' 'strip')

pkgver() {
  cd ninja
  local tag revs abbrev
  tag="$(git describe --tags --abbrev=0 origin/release)"
  revs="$(git rev-list --count "$tag..")"
  abbrev="$(git rev-parse --short HEAD)"
  echo "${tag#v}.r${revs}.g${abbrev}"
}

build() {
  export CC=/usr/bin/clang
  export CXX=/usr/bin/clang++
  export LD=/usr/bin/lld
  export AR=/usr/bin/llvm-ar
  export NM=/usr/bin/llvm-nm
  export RANLIB=/usr/bin/llvm-ranlib
  export STRIP=/usr/bin/llvm-strip
  export OBJCOPY=/usr/bin/llvm-objcopy
  export OBJDUMP=/usr/bin/llvm-objdump
  export READELF=/usr/bin/llvm-readelf

  cmake -S ninja -B build -DCMAKE_BUILD_TYPE=Release -Wno-dev -G 'Unix Makefiles' \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_CXX_COMPILER="/usr/bin/clang++" \
    -DCMAKE_CXX_FLAGS="-march=znver2 -mtune=znver2 -O3 -pipe -g0 -flto=thin" \
    -DCMAKE_EXE_LINKER_FLAGS="-fuse-ld=lld -O3 -Wl,--icf=all -Wl,--gc-sections -flto=thin" \
    -DBUILD_SHARED_LIBS=ON
  make ${MAKEFLAGS} -C build
}

strip_elfs() {
  while IFS= read -r -d '' f; do
    file -b "$f" | grep -q ELF || continue
    llvm-strip \
      --strip-unneeded \
      --keep-section=.eh_frame \
      --keep-section=.eh_frame_hdr \
      "$f"
  done < <(
    find "$pkgdir/usr/bin" -type f -perm -111 -print0
  )
}

package() {
  cd "$srcdir"
  DESTDIR="$pkgdir" make ${MAKEFLAGS} -C build install
  strip_elfs
}
