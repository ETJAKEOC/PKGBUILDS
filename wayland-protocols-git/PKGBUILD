_pkgname=wayland-protocols
pkgname="${_pkgname}-git"
pkgver=1.45.r16.g98f27dc
pkgrel=1
pkgdesc='Wayland protocols that add functionalities not available in the core protocol'
arch=('any')
url='https://wayland.freedesktop.org/'
license=('MIT')
makedepends=('git' 'wayland' 'meson' 'ninja')
conflicts=("${_pkgname}")
provides=("${_pkgname}")

source=("git+https://gitlab.freedesktop.org/wayland/wayland-protocols.git")
sha1sums=('SKIP')

pkgver() {
    cd "$_pkgname"
    git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    mkdir -p "${_pkgname}/build"
    arch-meson build "${_pkgname}" --buildtype=release
    ninja -C build
}

package() {
    DESTDIR="$pkgdir" ninja -C build install
    install -Dt "$pkgdir/usr/share/licenses/${_pkgname}" -m 644 "$_pkgname/COPYING"
}
