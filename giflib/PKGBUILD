pkgname=giflib
pkgver=5.2.2
pkgrel=2
pkgdesc='Library for reading and writing gif images'
url='http://giflib.sourceforge.net/'
arch=('x86_64')
license=('MIT')
depends=('glibc')
provides=('libgif.so')
makedepends=('git' 'xmlto' 'docbook-xml' 'docbook-xsl' 'imagemagick')
source=(${pkgname}::git+https://git.code.sf.net/p/giflib/code)
sha512sums=('SKIP')
options=(!zipman)

prepare() {
  cd ${pkgname}
  sed -e 's|giflib.xml|giflib.7|' \
      -re 's|doc/(.*).xml|doc/\1.1|g' \
      -i Makefile
}

build() {
  cd ${pkgname}
  CFLAGS="${CFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"
  make
}

package() {
  cd ${pkgname}
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -d "$pkgdir"/usr/share/man/man7
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
