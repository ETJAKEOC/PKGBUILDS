pkgname=libdrm-git
_realname=libdrm
pkgver=2.4.125.r2.g28917551
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services, master git version"
arch=(i686 x86_64)
license=('custom')
depends=('libpciaccess')
makedepends=('python-docutils' 'meson' 'git' 'ninja')
checkdepends=('cairo' 'cunit')
url="http://dri.freedesktop.org/"
provides=('libdrm')
conflicts=('libdrm')

source=('git+https://gitlab.freedesktop.org/mesa/drm.git')
sha512sums=('SKIP')

pkgver() {
    cd drm
    git describe --long --tags --abbrev=8 | sed 's/^libdrm-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  arch-meson "$srcdir/build" "$srcdir/drm" --reconfigure --prefix /usr --wrap-mode nofallback -D udev=true
  ninja -j4 -C "$srcdir/build"
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
