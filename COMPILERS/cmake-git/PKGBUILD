pkgname=cmake-git
pkgver=3.31.2.657.g54e998629d
pkgrel=1
pkgdesc='A cross-platform open-source make system'
arch=('x86_64')
url="http://www.cmake.org/"
license=('custom')
conflicts=('cmake')
provides=('cmake')
depends=('curl' 'libarchive' 'hicolor-icon-theme' 'jsoncpp' 'libjsoncpp.so' 'libuv' 'rhash' 'cppdap')
makedepends=('qt6-base' 'python-sphinx' 'nlohmann-json' 'git')
optdepends=('qt6-base: cmake-gui')
source=('git+https://gitlab.kitware.com/cmake/cmake.git' 'cmake-cppflags.patch')
md5sums=('SKIP' 'SKIP')

pkgver() {
    cd "$srcdir/cmake"
    git describe --always --tags --long | sed -e 's|^v||' -e 's|-|.|g'
}

prepare() {
    cd "$srcdir/cmake"
    patch -p1 -i ../cmake-cppflags.patch
}

build() {
  cd "$srcdir/cmake"
  export CXXFLAGS+=" ${CPPFLAGS}"
  ./bootstrap --prefix=/usr --mandir=share/man --docdir=share/doc/cmake --datadir=share/cmake \
  --sphinx-man --system-libs --qt-gui --parallel=4
  make -j4 LLVM=1 LLVM_IAS=1
}

package() {
  cd "$srcdir/cmake"
  make DESTDIR="${pkgdir}" install
  install -Dm644 Copyright.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
