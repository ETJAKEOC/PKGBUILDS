pkgname=ninja-git
epoch=2
pkgver=1.12.1.r139.gb78335e0
pkgrel=1
pkgdesc='Small build system with a focus on speed'
arch=(x86_64)
url='https://ninja-build.org/'
license=(Apache)
depends=(gcc-libs)
makedepends=(git cmake python re2c)
provides=(ninja)
conflicts=(ninja)
source=('git+https://github.com/ninja-build/ninja.git')
md5sums=('SKIP')

pkgver() {
  cd ninja
  local tag revs abbrev
  tag="$(git describe --tags --abbrev=0 origin/release)"
  revs="$(git rev-list --count "$tag..")"
  abbrev="$(git rev-parse --short HEAD)"
  echo "${tag#v}.r${revs}.g${abbrev}"
}

build() {
  cmake -S ninja -B build -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -Wno-dev -DCMAKE_C_COMPILER=clang -DCMAKE_CXX_COMPILER=clang++ \
    -DCMAKE_C_FLAGS="-march=bdver2 -mtune=bdver2 -O3 -fopenmp -flto -pipe -pthread -fPIC -g0" \
    -DCMAKE_CXX_FLAGS="-march=bdver2 -mtune=bdver2 -O3 -fopenmp -flto -pipe -pthread -fPIC -g0" \
    -DCMAKE_EXE_FLAGS="-fuse-ld=lld -fPIC -fopenmp -flto -O3 -pipe -pthread"
  cmake --build build
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd "$srcdir/build"
  install -Dm755 -t "$pkgdir/usr/bin" ninja

  cd "$srcdir/ninja"
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" COPYING
  install -Dm644 -t "$pkgdir/usr/share/doc/ninja" doc/manual.asciidoc
  install -Dm644 -t "$pkgdir/usr/share/vim/vimfiles/syntax" misc/ninja.vim
  install -Dm644 -t "$pkgdir/$site_packages" misc/ninja_syntax.py
  install -Dm644 misc/bash-completion "$pkgdir/usr/share/bash-completion/completions/ninja"
  install -Dm644 misc/zsh-completion "$pkgdir/usr/share/zsh/site-functions/_ninja"
}
