pkgname=libwebp
pkgver=1.5.0
pkgrel=1
pkgdesc="WebP library and conversion tools"
url="https://developers.google.com/speed/webp/"
arch=(x86_64)
license=(BSD-3-Clause)
depends=(giflib glibc libjpeg-turbo libpng libtiff)
makedepends=(cmake ninja)
provides=(libsharpyuv.so libwebp{,decoder,demux,mux}.so)
source=(
  https://storage.googleapis.com/downloads.webmproject.org/releases/webp/libwebp-${pkgver/[a-z]/-&}.tar.gz
  0001-cmake-Install-anim-utils.patch
)
sha256sums=('7d6fab70cf844bf6769077bd5d7a74893f8ffd4dfb42861745750c63c2a5c92c'
            '0c4ffe5d764ec0ceff04314616c00561271e3ff75ad4aa07bf8cac85028c79db')

prepare() {
  cd libwebp-$pkgver
  patch -Np1 -i ../0001-cmake-Install-anim-utils.patch
}

build() {
  local cmake_options=(
    -DBUILD_SHARED_LIBS=ON
    -DBUILD_STATIC_LIBS=ON
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_LIBDIR=/usr/lib
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_SKIP_INSTALL_RPATH=ON
    -DWEBP_BUILD_EXTRAS=OFF
    -DWEBP_BUILD_VWEBP=OFF
    -DWEBP_BUILD_{C,D,GIF2,IMG2}WEBP=ON
  )

  CFLAGS+=" -DNDEBUG"
  CXXFLAGS+=" -DNDEBUG"
  cmake -S libwebp-$pkgver -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 \
    libwebp-$pkgver/COPYING
}
