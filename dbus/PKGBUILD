pkgbase=dbus
pkgname=(dbus dbus-daemon-units dbus-docs)
pkgver=1.16.2
pkgrel=1
pkgdesc="Freedesktop.org message bus system"
url="https://gitlab.freedesktop.org/dbus/dbus"
arch=(x86_64)
license=("AFL-2.1 OR GPL-2.0-or-later")
depends=(audit expat glibc libcap-ng systemd-libs)
makedepends=(docbook-xsl doxygen git glib2 mallard-ducktype
  meson python qt5-tools systemd xmlto yelp-tools)
source=("git+${url}.git" 0001-Arch-Linux-tweaks.patch dbus-reload.hook)
b2sums=('SKIP' 'SKIP' 'SKIP')
options=('!debug' '!docs' '!emptydirs' '!staticlibs' 'ccache' 'buildflags' 'lto' 'makeflags' 'strip')

prepare() {
  cd dbus
  patch -Np1 -i ../0001-Arch-Linux-tweaks.patch
}

build() {
  local meson_options=(
    --reconfigure
    --optimization  3
    --auto-features enabled
    -Ddefault_library=both
    -Ddebug=false
    -Dstrip=true
    -Dapparmor=disabled
    -Ddbus_user=dbus
    -Dkqueue=disabled
    -Dlaunchd=disabled
    -Drelocation=disabled
    -Dselinux=disabled
    -Dx11_autolaunch=disabled
    -Dprefix=/usr
    -Dlibdir=lib
    -Db_pie=true
    -Db_staticpic=true
    -Db_lto=true
    -Dpython.bytecompile=2
    -Dbackend=ninja
    -Db_colorout=always
    -Dc_args="-march=znver2 -mtune=znver2 -pipe -O3 -g0"
    -Dc_link_args="-Wl,--as-needed -Wl,--gc-sections -fuse-ld=ld.lld"
    -Dcpp_args="-march=znver2 -mtune=znver2 -pipe -O3 -g0"
    -Dcpp_link_args="-Wl,--as-needed -Wl,--gc-sections -fuse-ld=ld.lld"
  )

  meson dbus build "${meson_options[@]}"
  ninja -C build
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_dbus() {
  depends+=(libaudit.so libcap-ng.so libexpat.so libsystemd.so)
  provides=(libdbus libdbus-1.so)
  conflicts=(libdbus)
  replaces=(libdbus)

  DESTDIR="$pkgdir" ninja install -C build

  _pick unit "$pkgdir"/usr/lib/systemd/{system,user}/dbus.service
  _pick docs "$pkgdir"/usr/share/doc

  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 *.hook

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 \
    dbus/COPYING dbus/LICENSES/AFL-2.1.txt
}

package_dbus-daemon-units() {
  pkgdesc+=" - Service units"
  depends=(dbus)
  provides=(dbus-units)
  conflicts=(dbus-broker-units)

  mv unit/* "$pkgdir"

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s dbus "$pkgdir/usr/share/licenses/$pkgname"
}

package_dbus-docs() {
  pkgdesc+=" - Documentation"

  mv docs/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 \
    dbus/COPYING dbus/LICENSES/AFL-2.1.txt
}
