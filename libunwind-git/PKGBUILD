pkgname=libunwind-git
pkgver=1.8.1.r65.g73f18010
pkgrel=1
pkgdesc="Portable and efficient C programming interface (API) to determine the call-chain of a program"
arch=('i686' 'x86_64')
url="https://www.nongnu.org/libunwind/"
license=('MIT')
depends=('glibc' 'xz' 'zlib')
makedepends=('git' 'texlive-core')
provides=("libunwind=$pkgver")
conflicts=('libunwind')
options=('staticlibs')
source=("git+https://github.com/libunwind/libunwind.git")
sha256sums=('SKIP')

pkgver() {
  cd "libunwind"
  _tag=$(git tag -l --sort -v:refname | grep -E '^v?[0-9\.]+$' | head -n1)
  _rev=$(git rev-list --count $_tag..HEAD)
  _hash=$(git rev-parse --short HEAD)
  printf "%s.r%s.g%s" "$_tag" "$_rev" "$_hash" | sed 's/^v//'
}

build() {
  cd "libunwind"
  autoreconf -fi
  ./configure --prefix="/usr"
  make LLVM=1 LLVM_IAS=1
}

package() {
  cd "libunwind"
  make DESTDIR="$pkgdir" install
  install -Dm644 "COPYING" -t "$pkgdir/usr/share/licenses/libunwind"
}
