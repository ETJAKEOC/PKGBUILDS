pkgname=firefox
pkgver=146.0a1+20251103.4+hd87cd04072b0
pkgrel=1
pkgdesc="Fast, Private & Safe Web Browser (Nightly version)"
url="https://www.mozilla.org/firefox/channel/desktop/#nightly"
arch=(x86_64)
license=(MPL-2.0)
depends=(alsa-lib at-spi2-core bash cairo dbus ffmpeg4.4 fontconfig freetype2 gcc-libs gdk-pixbuf2 glib2 glibc gtk3 hicolor-icon-theme hunspell-en_US libpulse libx11 libxcb libxcomposite libxdamage libxext libxfixes libxrandr libxss libxt mime-types nspr nss pango time ttf-font)
makedepends=(cbindgen clang compiler-rt diffutils dump_syms imake jack lld llvm mercurial mesa nasm nodejs python rust unzip wasi-compiler-rt wasi-libc wasi-libc++ wasi-libc++abi xorg-server-xvfb yasm zip)
optdepends=(
  'libnotify: Notification integration'
  'networkmanager: Location detection via available WiFi networks'
  'python: DMD (Dark Matter Detector) heap profiling'
  'speech-dispatcher: Text-to-Speech'
  'xdg-desktop-portal: Screensharing with Wayland'
)
options=(!emptydirs !lto !makeflags !buildflags strip)
_repo=https://hg.mozilla.org/mozilla-central
source=(
  hg+$_repo
  $pkgname-symbolic.svg
  $pkgname.desktop
  org.mozilla.$pkgname.metainfo.xml
)
validpgpkeys=(14F26682D0916CDD81E37B6D61B7B526D98F0353)
sha256sums=('SKIP'
            'a9b8b4a0a1f4a7b4af77d5fc70c2686d624038909263c795ecc81e0aec7711e9'
            'bff6d762411d77c9eea4f6fa10edd891e94366c44bd581054c62fe50df112eee'
            'ef8128868fc61282008ce1d82d0b1ef67588f9b92f7436debb84d42850b2d055'
)

pkgver() {
  cd mozilla-central
  local version=$(<browser/config/version_display.txt)
  local date=$(date +%Y%m%d) # Without TZ=UTC, to match systemd timer
  local counter=1
  local rev=$(hg id -i -r. | sed 's/+$//')
  local last_rev=${pkgver##*+h} tmp=${pkgver#*+}; tmp=${tmp%+*}
  local last_date=${tmp%.*} last_counter=${tmp#*.}
  if [[ $date == $last_date ]]; then
    if [[ $rev == $last_rev ]]; then
      counter=$last_counter
    else
      counter=$((last_counter + 1))
    fi
  fi
  echo $version+$date.$counter+h$rev
}

_env() {
  # Toolchain consistency
  export CC='/bin/clang'
  export CXX='/bin/clang++'
  export AR='/bin/llvm-ar'
  export NM='/bin/llvm-nm'
  export RANLIB='/bin/llvm-ranlib'
  export LD='/bin/ld.lld'
  export STRIP='/bin/llvm-strip'
  export OBJCOPY='/bin/llvm-objcopy'
  export LLVM_PROFDATA='/bin/llvm-profdata'
  export LLVM_SYMBOLIZER='/bin/llvm-symbolizer'

  # Generic tuning
  export JS_GC_SMALL_CHUNK_SIZE=4096
  export MOZ_JIT_THREAD_PRIORITY=high
  export MOZ_JIT_USE_FAST_ISOLATED_HEAPS=1
  export MOZ_JIT_THREAD_STACK_SIZE=4096
  export MOZ_JIT_OPT_LEVEL=3
  export MOZ_LTO_OPTLEVEL=3
  export MOZ_AVX2=1
  export MACH_BUILD_PYTHON_NATIVE_PACKAGE_SOURCE=pip
  export MOZBUILD_STATE_PATH="$srcdir/mozbuild"
  export MOZ_BUILD_DATE="$(date -u${SOURCE_DATE_EPOCH:+d @$SOURCE_DATE_EPOCH} +%Y%m%d%H%M%S)"
  export MOZ_NOSPAM=1
  export MOZ_SOURCE_REPO="$_repo"
  export MOZILLA_OFFICIAL=1
  export RUSTC_OPT_LEVEL=3
  export MOZ_UI_LOCALE=en-US

  # CPU and compiler tuning
  export CFLAGS="-O3 -g0 -march=znver2 -mtune=znver2 -pipe -fomit-frame-pointer -fno-plt -falign-functions=32 -falign-loops=32 -funroll-loops -fno-semantic-interposition"
  export CXXFLAGS="${CFLAGS} -fvisibility-inlines-hidden"
  export LDFLAGS="-Wl,-O3 -Wl,--as-needed -Wl,--sort-common -fuse-ld=lld -Wl,--icf=all -Wl,--no-keep-memory"
  export RUSTFLAGS="-C target-cpu=znver2 -C opt-level=3 -C link-arg=-fuse-ld=lld -C linker-plugin-lto -C codegen-units=1 -C debuginfo=0 -C strip=debuginfo"
  export LLVM=1
  export LLVM_IAS=1
}

prepare() {
  _env
  mkdir mozbuild
  echo -n "AIzaSyDwr302FpOSkGRpLlUpPThNTDPbXcIn_FM" > mozilla-central/google-api-key
  cat >mozconfig <<'END'
export MOZILLA_OFFICIAL=1
export RUSTC_OPT_LEVEL=3
export MOZ_UI_LOCALE=en-US
export CFLAGS="-O3 -g0 -march=znver2 -mtune=znver2 -pipe -fomit-frame-pointer -fno-plt -falign-functions=32 -falign-loops=32 -funroll-loops -fno-semantic-interposition"
export CXXFLAGS="${CFLAGS} -fvisibility-inlines-hidden"
export LDFLAGS="-Wl,-O3 -Wl,--as-needed -Wl,--sort-common -fuse-ld=lld -Wl,--icf=all -Wl,--no-keep-memory"
export RUSTFLAGS="-C target-cpu=znver2 -C opt-level=3 -C link-arg=-fuse-ld=lld -C linker-plugin-lto -C codegen-units=1 -C debuginfo=0 -C strip=debuginfo"

mk_add_options MOZ_PARALLEL_BUILD="$(nproc)"
mk_add_options MOZ_OBJDIR=@TOPSRCDIR@/obj

ac_add_options --allow-addon-sideload
ac_add_options --prefix=/usr

ac_add_options --with-unsigned-addon-scopes=app,system
ac_add_options --with-ccache=sccache
ac_add_options --with-wasi-sysroot=/usr/share/wasi-sysroot
ac_add_options --with-libclang-path="$(llvm-config --libdir)"
ac_add_options --with-google-location-service-api-keyfile=${PWD}/google-api-key
ac_add_options --with-google-safebrowsing-api-keyfile=${PWD}/google-api-key
ac_add_options --with-branding=browser/branding/nightly
ac_add_options --with-distribution-id=org.archlinux
ac_add_options --with-system-ffi
ac_add_options --with-system-gbm
ac_add_options --with-system-libdrm
ac_add_options --with-system-nspr
ac_add_options --with-system-nss
ac_add_options --with-system-pixman
ac_add_options --with-system-zlib
ac_add_options --with-toolchain-prefix="${CHOST}-"
ac_add_options --with-intl-api

ac_add_options --enable-application=browser
ac_add_options --enable-update-channel=nightly
ac_add_options --enable-install-strip
ac_add_options --enable-negotiateauth
ac_add_options --enable-geckodriver
ac_add_options --enable-system-policies
ac_add_options --enable-release
ac_add_options --enable-hardening
ac_add_options --enable-optimize=3
ac_add_options --enable-rust-simd
ac_add_options --enable-linker=lld
ac_add_options --enable-strip
ac_add_options --enable-lto=full
ac_add_options --enable-pgo
ac_add_options --enable-alsa
ac_add_options --enable-jack
ac_add_options --enable-jit
ac_add_options --enable-ui-locale=en_US
ac_add_options --enable-new-pass-manager
ac_add_options --enable-packed-relative-relocs
ac_add_options --enable-webrtc

ac_add_options --disable-debug
ac_add_options --disable-debug-symbols
ac_add_options --disable-tests
ac_add_options --disable-updater
ac_add_options --disable-crashreporter
ac_add_options --disable-default-browser-agent
ac_add_options --disable-tests
ac_add_options --disable-eme
ac_add_options --disable-wmf
ac_add_options --disable-parental-controls
ac_add_options --disable-disk-remnant-avoidance
ac_add_options --disable-bootstrap
ac_add_options --disable-cargo-incremental
END
}

build() {
  _env
  cd mozilla-central
  ulimit -n 4096
  time ./mach build
  cp -a /usr/bin/llvm-symbolizer "${srcdir}/mozilla-central/obj-x86_64-pc-linux-gnu/dist/bin/llvm-symbolizer"
}

package() {
  _env
  cd mozilla-central
  DESTDIR="$pkgdir" ./mach install
  local vendorjs="$pkgdir/usr/lib/$pkgname/browser/defaults/preferences/vendor.js"
  install -Dvm644 /dev/stdin "$vendorjs" <<END
pref("intl.locale.requested", "en_US");
pref("spellchecker.dictionary_path", "/usr/share/hunspell");
pref("browser.shell.checkDefaultBrowser", false);
pref("extensions.autoDisableScopes", 11);
pref("browser.gnome-search-provider.enabled", true);
END
  local distini="$pkgdir/usr/lib/$pkgname/distribution/distribution.ini"
  install -Dvm644 /dev/stdin "$distini" <<END
[Global]
id=archlinux
version=1.0
about=Mozilla Firefox for Arch Linux

[Preferences]
app.distributor=ETJAKEOC
app.distributor.channel=$pkgname
app.partner.archlinux=archlinux
END
  local i theme=nightly
  for i in 16 22 24 32 48 64 128 256; do
    install -Dvm644 browser/branding/$theme/default$i.png \
      "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/$pkgname.png"
  done
  install -Dvm644 browser/branding/$theme/content/about-logo.png \
    "$pkgdir/usr/share/icons/hicolor/192x192/apps/$pkgname.png"
  install -Dvm644 browser/branding/$theme/content/about-logo@2x.png \
    "$pkgdir/usr/share/icons/hicolor/384x384/apps/$pkgname.png"
  install -Dvm644 browser/branding/$theme/content/about-logo.svg \
    "$pkgdir/usr/share/icons/hicolor/scalable/apps/$pkgname.svg"
  install -Dvm644 ../$pkgname-symbolic.svg -t "$pkgdir/usr/share/icons/hicolor/symbolic/apps"
  install -Dvm644 ../$pkgname.desktop -t "$pkgdir/usr/share/applications"
  install -Dvm644 ../org.mozilla.$pkgname.metainfo.xml -t "$pkgdir/usr/share/metainfo"
  install -Dvm755 /dev/stdin "$pkgdir/usr/bin/$pkgname" <<END
#!/bin/sh
exec /usr/lib/$pkgname/firefox "\$@"
END
  local sprovider="$pkgdir/usr/share/gnome-shell/search-providers/$pkgname.search-provider.ini"
  install -Dvm644 /dev/stdin "$sprovider" <<END
[Shell Search Provider]
DesktopId=$pkgname.desktop
BusName=org.mozilla.${pkgname//-/_}.SearchProvider
ObjectPath=/org/mozilla/${pkgname//-/_}/SearchProvider
Version=2
END
}
