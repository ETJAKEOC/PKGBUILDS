pkgname=mercurial
pkgver=7.1.2
pkgrel=1
pkgdesc='A scalable distributed SCM tool'
arch=(x86_64)
url="https://www.mercurial-scm.org/"
license=(GPL)
depends=(python)
makedepends=(python-{build,installer,wheel}
             python-setuptools-scm
             python-docutils)
optdepends=('tk: for the hgk GUI')
backup=(etc/mercurial/hgrc)
source=(mercurial::hg+https://repo.mercurial-scm.org/hg/ mercurial.profile)
sha512sums=('SKIP' 'SKIP')
optiones=('!ccache' 'lto' 'buildflags' 'makeflags')

build() {
  cd $pkgname
  python -m build -wn
  LLVM=1 LLVM_IAS=1 make -C contrib/chg
}

package() {
  cd $pkgname
  python -m installer -d "$pkgdir" dist/*.whl
  make DESTDIR="$pkgdir" PREFIX=/usr install-doc
  install -Dm644 contrib/zsh_completion "$pkgdir/usr/share/zsh/site-functions/_hg"
  install -Dm644 contrib/bash_completion "$pkgdir/usr/share/bash-completion/completions/hg"
  make -C contrib/chg DESTDIR="$pkgdir" PREFIX=/usr install
  install -Dm755 contrib/hg-ssh "$pkgdir/usr/bin"
  install -Dm755 contrib/hgk "$pkgdir/usr/bin"
  install -Dm644 -t "$pkgdir/usr/share/emacs/site-lisp/" contrib/{mq.el,mercurial.el}
  install -Dm644 -t "$pkgdir/usr/share/vim/vimfiles/syntax/" contrib/vim/HGAnnotate.vim
  install -Dm644 "$srcdir/mercurial.profile" "$pkgdir/etc/profile.d/mercurial.sh"
  cat <<-EOF | install -Dm755 /dev/stdin "$pkgdir/etc/mercurial/hgrc"
	[web]
	cacerts = /etc/ssl/certs/ca-certificates.crt
	EOF
}
