pkgbase="dav1d-git"
_gitname=${pkgbase%-git}
pkgname=(dav1d-git dav1d-doc-git)
pkgver=1.5.1.r20.gaf5cf2b
pkgrel=1
pkgdesc="AV1 cross-platform decoder focused on speed and correctness"
url="https://code.videolan.org/videolan/dav1d"
license=(BSD-2-Clause)
arch=(x86_64)
makedepends=(doxygen git graphviz meson nasm ninja vulkan-headers xxhash)
source=("git+$url.git" "git+$url-test-data.git")
b2sums=('SKIP' 'SKIP')

prepare() {
  cd "$_gitname"
  ln -fs "$srcdir/dav1d-test-data" tests/dav1d-test-data
}

pkgver() {
  cd "$_gitname"
  git describe --long --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  arch-meson --prefix=/usr --buildtype=plain \
    -Dtestdata_tests=true \
    -Denable_docs=true \
    "$_gitname" build
  ninja -C build all doc/html
}

package_dav1d-git() {
  depends=(glibc)
  provides=(libdav1d.so dav1d)
  conflicts=(dav1d)
  optdepends=("dav1d-doc-git: HTML documentation")

  DESTDIR="$pkgdir" ninja install -C build

  cd "$_gitname"
  install -Dm 644 README.md CONTRIBUTING.md NEWS -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
}

package_dav1d-doc-git() {
  arch=(any)
  provides=(dav1d-doc)
  conflicts=(dav1d-doc)
  pkgdesc+=" (documentation)"

  install -d "${pkgdir}/usr/share/doc/${pkgbase}"
  cp -r build/doc/html -t "${pkgdir}/usr/share/doc/$pkgbase"

  cd "$_gitname"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/$pkgname"
}
