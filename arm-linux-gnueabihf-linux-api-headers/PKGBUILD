_target_arch=arm
_target=${_target_arch}-linux-gnueabihf
pkgname=${_target}-linux-api-headers
pkgver=6.16
rc=rc3
pkgrel=1
pkgdesc="Kernel headers sanitized for use in userspace"
arch=(any)
url='https://kernel.org/'
license=(GPL2)
makedepends=(rsync)
source=(https://git.kernel.org/pub/scm/linux/kernel/git/torvalds/linux.git/snapshot/linux-${pkgver}-${rc}.tar.gz)
sha256sums=('SKIP')
validpgpkeys=('ABAF11C65A2970B130ABE3C479BE3E4300411886'   # Linus Torvalds
              '647F28654894E3BD457199BE38DBBDC86092693E')  # Greg Kroah-Hartman

build() {
  cd linux-${pkgver}-${rc}
  make LLVM=1 LLVM_IAS=1 ARCH="${_target_arch}" mrproper
  make LLVM=1 LLVM_IAS=1 ARCH="${_target_arch}" headers
}

package() {
  cd linux-${pkgver}-${rc}
  make LLVM=1 LLVM_IAS=1 INSTALL_HDR_PATH="${pkgdir}"/usr/"${_target}" ARCH="${_target_arch}" headers_install
}
