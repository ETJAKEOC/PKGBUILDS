pkgname=proton-ETJAKEOC
pkgver=GE.Proton10.26
_geckover=2.47.4
_monover=10.4.0
_xaliaver=0.4.6
pkgrel=1
epoch=2
pkgdesc="Compatibility tool for Steam Play based on Wine and additional components, GloriousEggroll's custom build"
url="https://github.com/GloriousEggroll/proton-ge-custom"
arch=(x86_64)
license=('custom')
depends=(attr lib32-attr fontconfig lib32-fontconfig libxcursor lib32-libxcursor libxrandr lib32-libxrandr libxi lib32-libxi gettext lib32-gettext freetype2 lib32-freetype2 gcc-libs lib32-gcc-libs libpcap lib32-libpcap lzo lib32-lzo libxkbcommon lib32-libxkbcommon libvpx lib32-libvpx sdl2 lib32-sdl2 libsoup lib32-libsoup libgudev lib32-libgudev blas lib32-blas lapack lib32-lapack speex lib32-speex desktop-file-utils python steam-native-runtime cabextract)
makedepends=(autoconf bison perl flex mingw-w64-gcc git wget rsync unzip mingw-w64-tools lld nasm meson cmake fontforge afdko python-pefile glib2-devel glslang vulkan-headers \
  clang giflib lib32-giflib gnutls lib32-gnutls libxinerama lib32-libxinerama libxcomposite lib32-libxcomposite libxxf86vm lib32-libxxf86vm v4l-utils lib32-v4l-utils alsa-lib \
  lib32-alsa-lib libxcomposite lib32-libxcomposite mesa lib32-mesa mesa-libgl opencl-icd-loader lib32-opencl-icd-loader libpulse lib32-libpulse gtk3  lib32-gtk3 \
  gst-plugins-base-libs lib32-gst-plugins-base-libs vulkan-icd-loader lib32-vulkan-icd-loader sdl2  lib32-sdl2 rust  lib32-rust-libs libgphoto2 opencl-headers wayland-protocols)
optdepends=(giflib lib32-giflib gnutls lib32-gnutls v4l-utils lib32-v4l-utils libpulse lib32-libpulse alsa-plugins lib32-alsa-plugins alsa-lib lib32-alsa-lib libxcomposite lib32-libxcomposite libxinerama lib32-libxinerama opencl-icd-loader lib32-opencl-icd-loader gtk3 lib32-gtk3 gst-plugins-base-libs lib32-gst-plugins-base-libs vulkan-icd-loader lib32-vulkan-icd-loader libgphoto2)

provides=('proton')
options=(!ccache staticlibs !debug !emptydirs lto makeflags buildflags !strip)
sha256sums=('SKIP' 'SKIP' 'SKIP' 'SKIP')
source=(git+https://github.com/gloriouseggroll/proton-ge-custom.git
	https://dl.winehq.org/wine/wine-gecko/${_geckover}/wine-gecko-${_geckover}-x86{,_64}.tar.xz
	https://github.com/madewokherd/wine-mono/releases/download/wine-mono-${_monover}/wine-mono-${_monover}-x86.tar.xz
	https://github.com/madewokherd/xalia/releases/download/xalia-${_xaliaver}/xalia-${_xaliaver}-net9-windows.zip)
noextract=(wine-gecko-${_geckover}-{x86,x86_64}.tar.xz
	wine-mono-${_monover}-x86.tar.xz
	xalia-${_xaliaver}-net9-windows.zip)

pkgver() {
	cd proton-ge-custom
	local tag
	tag=$(git tag -l 'GE-Proton*' --sort=-v:refname | head -n 1)
	[[ -z "$tag" ]] && tag="GE-Proton10-26"
	printf "%s" "${tag//-/.}"
	git submodule update --init --recursive
}

prepare() {
	rm -rf wrappers && mkdir wrappers
	cd proton-ge-custom
	mkdir -p contrib
	mv "$srcdir"/wine-gecko-${_geckover}-x86{,_64}.tar.xz contrib/
	mv "$srcdir"/wine-mono-${_monover}-x86.tar.xz contrib/
	mv "$srcdir"/xalia-${_xaliaver}-net9-windows.zip contrib/
	git submodule update --init --recursive
	sed -i 's/TryContainer/#TryContainer/g' configure.sh
	sed -i 's/containerengine=/#containerengine=/g' configure.sh
	sed -i '/source-han-sans/d' configure.sh
	sed -i "s/net48-mono/net9-windows/g" Makefile.in
	sed -i '/CONTAINERGOALS :=/,+1 s/.*$/CONTAINERGOALS :=/' Makefile
	./patches/protonprep-valve-staging.sh
	export RUSTUP_TOOLCHAIN=nightly
	export CARGO_HOME="${SRCDEST}/proton-cargo"
	for rustlib in gst-plugins-rs; do
		pushd $rustlib
		cargo fetch --target i686-unknown-linux-gnu
		cargo fetch --target x86_64-unknown-linux-gnu
		popd
	done
}

build() {
	cd proton-ge-custom
	if [[ ! -d build ]]; then
	  mkdir "build"
	  cd "build"
	else
	  cd "build"
	fi
	export CCACHE_DISABLE=1
	export CCACHE_DIR=
	export RUSTUP_TOOLCHAIN=nightly
	export CARGO_HOME="${SRCDEST}/proton-cargo"
	export PROTON_SKIP_CJK_FONTS=1
	export PROTON_NO_CONTAINER=1
	export PROTON_SKIP_CJK_FONTS=1
	unset DISPLAY
	../configure.sh --build-name="${pkgname}"
	mkdir -p build/obj-fonts/source-han
	touch build/obj-fonts/source-han/{msyh,malgun,simsun,nsimsun}.ttf
	PROTON_SKIP_CJK_FONTS=1 LLVM=1 LLVM_IAS=1 make -j$(nproc)
}

package() {
    cd "${srcdir}/proton-ge-custom/build"

    rm -rf dst-* obj-* src-* pfx-*

    local _compatdir="$pkgdir/usr/share/steam/compatibilitytools.d"
    mkdir -p "$_compatdir/${pkgname}"

    rsync --delete -arx dist/* "$_compatdir/${pkgname}"

    #
    # 32-bit vkd3d DLLs
    #
    mkdir -p "$_compatdir/${pkgname}/files/lib/i386-w64-mingw32/vkd3d"
    cp /usr/i686-w64-mingw32/bin/{libgcc_s_dw2-1.dll,libwinpthread-1.dll} \
        "$_compatdir/${pkgname}/files/lib/i386-w64-mingw32/vkd3d/"

    #
    # 64-bit vkd3d DLLs
    #
    mkdir -p "$_compatdir/${pkgname}/files/lib/x86_64-w64-mingw32/vkd3d"
    cp /usr/x86_64-w64-mingw32/bin/{libgcc_s_seh-1.dll,libwinpthread-1.dll} \
        "$_compatdir/${pkgname}/files/lib/x86_64-w64-mingw32/vkd3d/"

    #
    # License
    #
    mkdir -p "$pkgdir/usr/share/licenses/${pkgname}"
    mv "$_compatdir/${pkgname}"/LICENSE{,.OFL} \
        "$pkgdir/usr/share/licenses/${pkgname}"

    #
    # Strip binaries
    #
    cd "$_compatdir/${pkgname}/files"

# Strip only 32-bit Windows binaries in i386-windows dirs
i686_w32_targets=$(find lib/wine -type f \( -path '*/i386-windows/*' -o -path '*/i386-w64-mingw32/*' \) \( -iname '*.dll' -o -iname '*.exe' \) -print)
if [ -n "$i686_w32_targets" ]; then
  i686-w64-mingw32-strip --strip-unneeded $i686_w32_targets || true
fi

# Strip only 64-bit Windows binaries in x86_64-windows dirs
x86_64_w32_targets=$(find lib/wine -type f \( -path '*/x86_64-windows/*' -o -path '*/x86_64-w64-mingw32/*' \) \( -iname '*.dll' -o -iname '*.exe' \) -print)
if [ -n "$x86_64_w32_targets" ]; then
  x86_64-w64-mingw32-strip --strip-unneeded $x86_64_w32_targets || true
fi

# Also handle dxvk/vkd3d/nvapi subtrees explicitly if present
for sub in dxvk vkd3d-proton nvapi; do
  if [ -d "lib/wine/$sub/x86_64-windows" ]; then
    x86_64-w64-mingw32-strip --strip-unneeded $(find "lib/wine/$sub/x86_64-windows" -type f \( -iname '*.dll' -o -iname '*.exe' \)) || true
  fi
  if [ -d "lib/wine/$sub/i386-windows" ]; then
    i686-w64-mingw32-strip --strip-unneeded $(find "lib/wine/$sub/i386-windows" -type f \( -iname '*.dll' -o -iname '*.exe' \)) || true
  fi
done
}
