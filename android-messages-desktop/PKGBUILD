pkgname=android-messages-desktop
pkgver=5.7.1.r3.g13e1259
pkgrel=1
_electronversion=37
pkgdesc="Android Messages as a cross-platform desktop app"
arch=('x86_64')
url="https://github.com/OrangeDrangon/android-messages-desktop"
license=('MIT')
depends=("electron${_electronversion}")
makedepends=('git' 'yarn' 'clang' 'lld' 'nodejs' 'npm')
source=("git+${url}.git" "${pkgname}.sh" "${pkgname}.desktop")
sha256sums=('SKIP' 'SKIP' 'SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  set -o pipefail
  git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//g' ||
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

prepare() {
  cd "$pkgname"
  sed -i "s|@ELECTRONVERSION@|${_electronversion}|" "$srcdir/$pkgname.sh"
  if [[ -f electron-builder.js ]]; then
    sed -i '1s|^export default {|module.exports = {|' electron-builder.js
  fi
}

build() {
  export CC=clang
  export CXX=clang++
  export CFLAGS="-march=znver2 -mtune=znver2 -O3 -pipe -flto"
  export CXXFLAGS="$CFLAGS"
  export LDFLAGS="-fuse-ld=lld -flto"
  export NM=llvm-nm
  export AR=llvm-ar
  export RANLIB=llvm-ranlib
  export NODE_OPTIONS="--max-old-space-size=8192 --trace-warnings"
  export WEBPACK_WORKER_POOL=8
  export YARN_CACHE_FOLDER="$srcdir/.yarn-cache"
  export YARN_ENABLE_IMMUTABLE_INSTALLS=1
  dist="$srcdir/dist"
  electronDist="/usr/lib/electron${_electronversion}"
  electronVer="$(sed s/^v// /usr/lib/electron${_electronversion}/version)"

  cd "$pkgname"
  mkdir -p "$dist"
  rm -f package-lock.json yarn.lock
  ncu -u
  yarn install --network-concurrency 10 --mutex network
  find node_modules -type f -name '*.node' -exec llvm-strip --strip-unneeded {} + 2>/dev/null || true
  yarn build --mode production --parallelism 8
  yarn electron-builder --linux --x64 --dir \
    --config electron-builder.js \
    -c.electronVersion="${electronVer}" \
    -c.electronDist="$electronDist" \
    -c.removePackageScripts=true \
    -c.nodeGypRebuild=false \
    -c.buildDependenciesFromSource=false \
    -c.asarUnpack=[] \
    -c.compression=store
}

package() {
  cd "$pkgname"
  install -Dm644 dist/linux-unpacked/resources/app.asar -t "$pkgdir/usr/lib/$pkgname/resources/"
  install -Dm755 "$srcdir/$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "$srcdir/$pkgname.desktop" -t "$pkgdir/usr/share/applications"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  for i in 16 24 32 48 64 128 256 512 1024; do
    install -Dm644 "resources/icons/${i}x${i}.png" \
      "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/$pkgname.png"
  done
}
