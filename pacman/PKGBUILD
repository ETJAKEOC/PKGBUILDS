pkgname=pacman-ETJAKEOC
pkgver=7.0.0.r79.g655be2f
pkgrel=1
pkgdesc="A library-based package manager with dependency support - with proper support, no Rust ever!"
arch=('x86_64')
url="https://www.archlinux.org/pacman/"
license=('GPL-2.0-or-later')
depends=(bash coreutils curl libcurl.so gawk gettext glibc gnupg
  gpgme libgpgme.so grep libarchive libarchive.so openssl libcrypto.so
  pacman-mirrorlist systemd)
makedepends=(asciidoc doxygen git meson)
checkdepends=(fakechroot python)
provides=('libalpm.so' 'pacman')
conflicts=('libalpm.so' 'pacman')
backup=(etc/pacman.conf etc/makepkg.conf)
source=("git+https://gitlab.archlinux.org/pacman/pacman.git"
        pacman.conf makepkg.conf alpm.sysusers)
sha256sums=('SKIP' 'SKIP' 'SKIP' 'SKIP')

pkgver() {
  cd "$srcdir/pacman"
  git describe --abbrev=7 --match 'v*' | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/pacman"

  arch-meson \
        --buildtype=plain \
        -Ddoc=disabled \
        -Ddoxygen=disabled \
        -Dscriptlet-shell=/usr/bin/bash \
        -Dldconfig=/usr/bin/ldconfig \
        build

  ninja -C build
}

package() {
  cd "$srcdir/pacman"

  DESTDIR="$pkgdir" ninja install -C build

  # install Arch specific stuff
  install -dm755 "$pkgdir/etc"
  install -m644 "$srcdir/pacman.conf" "$pkgdir/etc"
  install -m644 "$srcdir/makepkg.conf" "$pkgdir/etc"
  install -D -m644 "$srcdir/alpm.sysusers" "${pkgdir}"/usr/lib/sysusers.d/alpm.conf

  local wantsdir="$pkgdir/usr/lib/systemd/system/sockets.target.wants"
  install -dm755 "$wantsdir"

  local unit
  for unit in dirmngr gpg-agent gpg-agent-{browser,extra,ssh} keyboxd; do
    ln -s "../${unit}@.socket" "$wantsdir/${unit}@etc-pacman.d-gnupg.socket"
  done
}
