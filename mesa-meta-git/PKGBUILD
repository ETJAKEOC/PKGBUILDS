pkgname=mesa-meta-git
pkgdesc="an open-source implementation of the OpenGL specification, git version, both x86_64 and i386 in one metapackage"
pkgver=26.0.0_devel.d41d8cd
pkgrel=1
arch=('x86_64')
makedepends=('git' 'xorgproto' 'meson' 'libxml2' 'libvdpau' 'libva' 'elfutils' 'libxrandr' 'glslang' 'directx-headers-git' 'python-mako' 'python-ply' 'cbindgen' 'wayland-protocols' 'python-packaging' 'python-pyaml' 'lib32-glibc' 'lib32-libdrm' 'lib32-vulkan-icd-loader')
depends=('libdrm' 'libxxf86vm' 'libxdamage' 'libxshmfence' 'libelf' 'libglvnd' 'wayland' 'lm_sensors' 'vulkan-icd-loader' 'zstd' 'expat' 'gcc-libs' 'libxfixes' 'libx11' 'systemd-libs' 'libxext' 'libxcb' 'glibc' 'zlib' 'python' 'xcb-util-keysyms')
optdepends=('opengl-man-pages: for the OpenGL API man pages')
provides=('vulkan-mesa-layers' 'opencl-driver' 'opengl-driver' 'vulkan-driver' 'vulkan-radeon' 'vulkan-swrast' 'vulkan-virtio' 'libva-mesa-driver' 'mesa-vdpau' 'mesa-libgl' 'mesa' 'lib32-mesa' 'mesa-git' 'lib32-mesa-git')
conflicts=($provides)
url="https://www.mesa3d.org"
license=('custom')
source=('mesa::git+https://gitlab.freedesktop.org/mesa/mesa.git')
sha256sums=('SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP')
options=('!debug' '!docs' '!emptydirs' '!staticlibs' 'ccache' 'buildflags' '!lto' 'makeflags' 'strip')

declare -A _crates=(
   proc-macro2    1.0.105
   quote          1.0.43
   syn            2.0.114
   unicode-ident  1.0.22
)

for _crate in "${!_crates[@]}"; do
  source+=($_crate-${_crates[$_crate]}.tar.gz::https://crates.io/api/v1/crates/$_crate/${_crates[$_crate]}/download)
done

pkgver() {
    cd mesa
    local _ver
    _ver=$(<VERSION)
    local _patchver
    local _patchfile
    for _patchfile in "$source[@]"; do
        _patchfile="$_patchfile%%::*"
        _patchfile="$_patchfile##*/"
        [[ $_patchfile = *.patch ]] || continue
        _patchver="$_patchver$(md5sum $srcdir/$_patchfile | cut -c1-32)"
    done
    _patchver="$(echo -n $_patchver | md5sum | cut -c1-7)"
    echo ${_ver/-/_}.$_patchver
}

prepare() {
    if [  -d build ]; then
        rm -rf build/*
    else
        mkdir build
    fi
    if [  -d build32 ]; then
        rm -rf build32/*
    else
        mkdir build32
    fi
    local _patchfile
    for _patchfile in "$source[@]"; do
        _patchfile="$_patchfile%%::*"
        _patchfile="$_patchfile##*/"
        [[ $_patchfile = *.patch ]] || continue
        echo "Applying patch $_patchfile..."
        patch --directory=mesa --forward --strip=1 --input="$srcdir/$_patchfile"
    done
    cd mesa
    git submodule update --init --recursive
}

build () {
  export CC=/usr/bin/clang
  export CXX=/usr/bin/clang++
  export CPP=/usr/bin/clang-cpp
  export LD=/usr/bin/lld
  export CC_LD=/usr/bin/lld
  export CXX_LD=/usr/bin/lld
  export AR=/usr/bin/llvm-ar
  export NM=/usr/bin/llvm-nm
  export STRIP=/usr/bin/llvm-strip
  export OBJCOPY=/usr/bin/llvm-objcopy
  export OBJDUMP=/usr/bin/llvm-objdump
  export READELF=/usr/bin/llvm-readelf
  export RANLIB=/usr/bin/llvm-ranlib
  export LLVM_VERSION=23

local _meson_flags=(
  # Core Meson / Build System Setup
  -D prefix=/usr
  -D backend=ninja
  -D default_library=shared
  -D b_colorout=always

  # Build Optimization & Hardening
  -D b_pie=true
  -D b_staticpic=true
  -D b_ndebug=true
  -D python.bytecompile=2
  -D microsoft-clc=disabled

  # Graphics APIs (OpenGL / EGL / GLX)
  -D egl=enabled
  -D opengl=true
  -D gbm=enabled
  -D gles1=disabled
  -D gles2=enabled
  -D glx=dri
  -D glx-direct=true
  -D glvnd=enabled

  # Platforms & Windowing Systems
  -D platforms=x11,wayland
  -D xlib-lease=enabled

  # Gallium Core
  -D gallium-rusticl=false
  -D gallium-extra-hud=true
  -D gallium-va=enabled

  # Disabled Gallium / D3D / Media Paths
  -D gallium-d3d12-graphics=disabled
  -D gallium-d3d12-video=disabled
  -D gallium-d3d10umd=false
  -D gallium-mediafoundation=disabled

  # Vulkan Layers & Features
  -D vulkan-layers=device-select,overlay,screenshot
  -D vulkan-beta=true

  # Video Acceleration & Codec Support
  -D video-codecs=h264dec,h264enc,h265dec,h265enc,vc1dec,vp9dec

  # Shader Cache & Compression
  -D shader-cache=enabled
  -D shader-cache-max-size=16G
  -D zstd=enabled

  # Debugging, Testing & Instrumentation
  -D build-tests=false
  -D valgrind=disabled
  -D libunwind=enabled

  # System Integration & Misc Options
  -D lmsensors=enabled
  -D android-libbacktrace=disabled
  -D intel-rt=disabled
  -D tools=[]
)

    meson build mesa "${_meson_flags[@]}" -D libdir=/usr/lib -D amd-use-llvm=true -D draw-use-llvm=true -D b_lto=true -D llvm=enabled -D shared-llvm=enabled -D gallium-drivers=radeonsi,virgl,svga,llvmpipe -D vulkan-drivers=amd,swrast,virtio,gfxstream \
        -D c_args="$CFLAGS" -D c_link_args="$LDFLAGS" -D cpp_args="$CPPFLAGS" -D cpp_link_args="$LDFLAGS"
    ninja -j$(nproc) -C build

    meson build32 mesa "${_meson_flags[@]}" -D libdir=/usr/lib32 -D amd-use-llvm=false -D draw-use-llvm=false -D b_lto=false -D llvm=disabled -D shared-llvm=disabled -D gallium-drivers=radeonsi,virgl,svga,softpipe -D vulkan-drivers=virtio,gfxstream \
        -D c_args="-m32 $CFLAGS" -D c_link_args="-m32 $LDFLAGS" -D cpp_args="-m32 $CPPFLAGS" -D  cpp_link_args="-m32 $LDFLAGS"
    ninja -j$(nproc) -C build32
}

package() {
    mkdir -p "$pkgdir/usr/bin" "$pkgdir/usr/lib" "$pkgdir/usr/lib32" "$pkgdir/usr/share"
    DESTDIR="$pkgdir" ninja -j$(nproc) -C build32 install
    DESTDIR="$pkgdir" ninja -j$(nproc) -C build install
    ln -s usr/lib32/libGLX_mesa.so.0 "$pkgdir/usr/lib32/libGLX_indirect.so.0"
    ln -s usr/lib/libGLX_mesa.so.0 "$pkgdir/usr/lib/libGLX_indirect.so.0"
}
