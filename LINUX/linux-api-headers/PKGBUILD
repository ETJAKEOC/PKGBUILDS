pkgname=linux-api-headers
pkgver=6.13
rcver=rc3
pkgrel=1
pkgdesc='Kernel headers sanitized for use in userspace'
arch=('x86_64')
url='https://git.kernel.org/pub/scm/linux/kernel/git/torvalds/linux.git/snapshot/'
license=(GPL-2.0-only)
makedepends=(rsync)
source=(${url}/linux-${pkgver}-${rcver}.tar.gz)
sha256sums=('SKIP')
validpgpkeys=('ABAF11C65A2970B130ABE3C479BE3E4300411886'   # Linus Torvalds
              '647F28654894E3BD457199BE38DBBDC86092693E')  # Greg Kroah-Hartman

build() {
  cd linux-${pkgver}-${rcver}
  make LLVM=1 LLVM_IAS=1 -j4 mrproper
}

package() {
  cd linux-${pkgver}-${rcver}
  make INSTALL_HDR_PATH="${pkgdir}/usr" headers_install
  rm -r "${pkgdir}/usr/include/drm"
}

